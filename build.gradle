plugins {
    id "com.avast.gradle.docker-compose" version "0.9.1"
    id("maven-publish")
}

apply plugin: 'groovy'

group 'com.codebrig.arthur'
version '0.4'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/CodeBrig/Arthur")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.grakn.ai/repository/maven/'
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'
    implementation files("lib/bblfsh-client-1.11.1.jar")
    implementation 'org.bblfsh:bblfsh-client:1.11.0'
    implementation group: 'org.kohsuke', name: 'github-api', version: '1.101'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.1.3.201810200350-r'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    implementation (group: 'io.grakn.client', name: 'grakn-client', version: "2.0.0-alpha-4") {
        exclude group: 'io.graql', module: 'graql-grammar'
    }
    implementation group: 'com.google.guava', name: 'guava', version: '30.0-jre'
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/arthur_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

test {
    dependsOn "composeUp"
    finalizedBy "composeDown"

    testLogging {
        events "passed", "skipped", "failed"
        setExceptionFormat("full")

        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

dockerCompose {
    useComposeFiles = ['./docker-compose.yml']
    dockerComposeWorkingDirectory = './docker/'
    captureContainersOutput = true
}