plugins {
    id "com.avast.gradle.docker-compose" version "0.9.1"
}

apply plugin: 'groovy'

group 'com.codebrig.arthur'
version '0.4'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.grakn.ai/repository/maven/'
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    implementation 'org.bblfsh:bblfsh-client:1.11.0'
    implementation group: 'org.kohsuke', name: 'github-api', version: '1.101'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.1.3.201810200350-r'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
    implementation group: 'io.graql', name: 'graql-lang', version: "1.0.6"
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/arthur_build.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

test {
    dependsOn "composeUp"
    finalizedBy "composeDown"
}

dockerCompose {
    useComposeFiles = ['./docker-compose.yml']
    dockerComposeWorkingDirectory = './docker/'
    captureContainersOutput = true
}